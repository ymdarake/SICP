#lang racket

;;;; Chapter5.2 - A Register-Machine Simulator

;;; 5.2.1 - The Machine Model
;; The machine model generated by make-machine is represented as
;; a procedure with local state using the message-passing techniques developed in Chapter 3.

; Make-machine then extends this basic model (by sending it messages) to include the registers, operations, and controller of the particular machine being defined.
; First it allocates a register in the new machine for each of the supplied register names and installs the designated operations in the machine. 
; Then it uses an assembler (described below in 5.2.2) to transform the controller list into instructions for the new machine and installs these as the machine’s instruction sequence.
; Make-machine returns as its value the modified machine model.
(define (make-machine register-names 
                      ops 
                      controller-text)
  (let ((machine (make-new-machine)))
    (for-each (lambda (register-name)
                ((machine 'allocate-register) 
                 register-name))
              register-names)
    ((machine 'install-operations) ops)
    ((machine 'install-instruction-sequence)
     (assemble controller-text machine))
    machine))

; Registers
(define (make-register name)
  (let ((contents '*unassigned*))
    (define (dispatch message)
      (cond ((eq? message 'get) contents)
            ((eq? message 'set)
             (lambda (value)
               (set! contents value)))
            (else
             (error "Unknown request: REGISTER" message))))
    dispatch))

(define (get-contents register)
  (register 'get))
(define (set-contents! register value)
  ((register 'set) value))

; The stack
; We can also represent a stack as a procedure with local state.
; make-stack creates a stack whose local state consists of a list of the items on the stack.
; 1. push, 2. pop, 3. initialize.
(define (make-stack)
  (let ((s '()))
    (define (push x)
      (set! s (cons x s)))
    (define (pop)
      (if (null? s)
          (error "Empty stack: POP")
          (let ((top (car s)))
            (set! s (cdr s))
            top)))
    (define (initialize)
      (set! s '())
      'done)
    (define (dispatch message)
      (cond ((eq? message 'push) push)
            ((eq? message 'pop) (pop))
            ((eq? message 'initialize) (initialize))
            (else (error "Unknown request: STACK" message))))
    dispatch))

; The basic machine
(define (make-new-machine)
  (let ((pc (make-register 'pc))
        (flag (make-register 'flag))
        (stack (make-stack))
        (the-instruction-sequence '()))
    (let ((the-ops
           (list
            (list 'initialize-stack (lambda () (stack 'initialize)))))
          (register-table
           (list
            (list 'pc pc)
            (list 'flag flag))))
      (define (allocate-register name)
        (if (assoc name register-table)
            (error "Multiply defined register: " name)
            (set! register-table
                  (cons
                   (list name (make-register name))
                   register-table)))
        'register-allocated)
      (define (lookup-register name)
        (let ((val (assoc name register-table)))
          (if val
              (cadr val)
              (error "Unknown register: " name))))
      (define (execute)
        (let ((insts (get-contents pc)))
          (if (null? insts)
              'done
              (begin
                ((instruction-execution-proc (car insts)))
                (execute)))))
      (define (dispatch message)
        (cond ((eq? message 'start) (set-contents! pc the-instruction-sequence)
                                    (execute))
              ((eq? message 'install-instruction-sequence) (lambda (seq) (set! the-instruction-sequence seq)))
              ((eq? message 'allocate-register) allocate-register)
              ((eq? message 'get-register) lookup-register)
              ((eq? message 'install-operations) (lambda (ops) (set! the-opes (append the-ops ops))))
              ((eq? message 'stack) stack)
              ((eq? message 'operations) the-ops)
              (else (error "Unknown request: MACHINE " message))))
      dispatch)))

; The flag register is used to control branching in the simulated machine.
; - Test instructions set the contents of flag to the result of the test (true or false)
; - Branch instructions decide whether or not to branch by examining the contents of flag.
; The pc register determines the sequencing of instructions as the machine runs.
; - This sequencing is implemented by the internal procedure execute.
; - As part of its operation, each instruction execution procedure modifies pc
; - to indicate the next instruction to be executed.
; - Branch and goto instructions change pc to point to the new destination.

(define (start machine)
  (machine 'start))

(define (get-register-contents 
         machine register-name)
  (get-contents 
   (get-register machine register-name)))

(define (set-register-contents! 
         machine register-name value)
  (set-contents! 
   (get-register machine register-name) 
   value)
  'done)

(define (get-register machine reg-name)
  ((machine 'get-register) reg-name))


;;; 5.2.2 - The Assembler

;; The assembler transforms the sequence of controller expressions for a machine
; into a corresponding list of machine instructions, each with its execution procedure.
; - Overall, the assembler is much like the evaluators we studied in Chapter 4:
; - there is an input language (in this case, the register-machine language) and
; - we must perform an appropriate action for each type of expression in the language.

; As it scans the text, it constructs both a list of instructions and a table that associates each label with a pointer into that list.
; Then the assembler augments the instruction list by inserting the execution procedure for each instruction.
; - Assemble calls extract-labels to build the initial instruction list and label table from the supplied controller text.
; - The second argument to extract-labels is a procedure to be called to process these results.
(define (assemble controller-text machine)
  (extract-labels controller-text
                  (lambda (insts labels)
                    (update-insts! insts labels machine)
                    insts)))

; Extract-labels takes as arguments a list text (the sequence of controller instruction expressions) and a receive procedure.
; Receive(↑lamda written up there) will be called with two values:
; - (1) a list insts of instruction data structures, each containing an instruction from text.
; - (2) a table called labels, which associates each label from text with the position in the list insts that the label designates.
(define (extract-labels text receive)
  (if (null? text)
      (receive '() '())
      (extract-labels
       (cdr text)
       (lambda (insts labels)
         (let ((next-inst (car text)))
           (if (symbol? next-inst)
               (receive insts (cons (make-label-entry next-inst insts) labels))
               (receive (cons (make-instruction next-inst) insts) labels)))))))

; update-insts! modifies the instruction list,
; - which initially contains only the text of the instructions,
; - to include the corresponding execution procedures:
(define (update-insts! insts labels machine)
  (let ((pc (get-register machine 'pc))
        (flag (get-register machine 'flag))
        (stack (machine 'stack))
        (ops (machine 'operations)))
    (for-each
     (lambda (inst)
       (set-instruction-execution-proc!
        inst
        (make-execution-procedure
         (instruction-text inst)
         labels
         machine
         pc
         flag
         stack
         ops)))
     insts)))

; The machine instruction data structure simply pairs the instruction text with the corresponding execution procedure.
(define (make-instruction text) (cons text '()))
(define (instruction-text inst) (car inst))
(define (instruction-execution-proc inst) (cdr inst))
(define (set-instruction-execution-proc! inst proc) (set-cdr! inst proc))

; Elements of the label table are pairs:
(define (make-label-entry label-name insts)
  (cons label-name insts))
; Entries will be looked up in the table with
(define (lookup-label labels label-name)
  (let ((val (assoc label-name labels)))
    (if val
        (cdr val)
        (error "Undefined label: ASSEMBLE" label-name))))

; Exercise 5.8
;start
;  (goto (label here))
;here
;  (assign a (const 3))
;  (goto (label there))
;here
;  (assign a (const 4))
;  (goto (label there))
;there
